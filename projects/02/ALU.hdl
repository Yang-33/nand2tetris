// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:
    // Transformes X by Mux4Way16
    Not16(in=x, out=flipx);
    Mux4Way16(sel[0]=zx, sel[1]=nx, a=x, b[0..15]=false, c=flipx, d[0..15]=true, out=outx);
    
    // Transformes Y by Mux4Way16
    Not16(in=y, out=flipy);
    Mux4Way16(sel[0]=zy, sel[1]=ny, a=y, b[0..15]=false, c=flipy, d[0..15]=true, out=outy);

    // Add or And
    And16(a=outx, b=outy, out=outand);
    Add16(a=outx, b=outy, out=outadd);
    Mux16(a=outand, b=outadd, sel=f, out=outf);

    // Flip
    Not16(in=outf, out=flipoutf);

    // Output : out, ng
    Mux16(a=outf, b=flipoutf, sel=no, out=out, out[0..7]=outlow, out[8..15]=outup, out[15]=ng);

    // Output : zr
    Or8Way(in=outlow, out=lowor);
    Or8Way(in=outup, out=upor);
    Or(a=lowor, b=upor, out=outbitor);
    Not(in=outbitor, out=zr);

}